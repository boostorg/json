#
# Copyright (c) 2019 Vinnie Falco (vinnie.falco@gmail.com)
#
# Distributed under the Boost Software License, Version 1.0. (See accompanying
# file LICENSE_1_0.txt or copy at http://www.boost.org/LICENSE_1_0.txt)
#
# Official repository: https://github.com/vinniefalco/json
#

cmake_minimum_required(VERSION 3.5...3.16)

set(BOOST_JSON_VERSION 1)
if(BOOST_SUPERPROJECT_VERSION)
    set(BOOST_JSON_VERSION ${BOOST_SUPERPROJECT_VERSION})
endif()

project(boost_json VERSION "${BOOST_JSON_VERSION}" LANGUAGES CXX)

option(BOOST_JSON_STANDALONE "Build boost::json as a static standalone library" FALSE)

# this option allows consumers of the library to skip building tests and examples.
# For example, they might be cross-compiling or not want to bring in the submodules required by bench
option(BOOST_JSON_ALLOW_TESTS "Allow the tests, examples and benchmarks to be built" ON)

file(GLOB_RECURSE BOOST_JSON_HEADERS CONFIGURE_DEPENDS
    include/boost/*.hpp
    include/boost/*.ipp
    include/boost/*.natvis
)

set(BOOST_JSON_SOURCES
    ${CMAKE_CURRENT_SOURCE_DIR}/src/src.cpp
)

set_property(GLOBAL PROPERTY USE_FOLDERS ON)

if(${CMAKE_VERSION} VERSION_GREATER 3.7.2)
    source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR}/include/boost PREFIX "" FILES ${BOOST_JSON_HEADERS})
    source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR}/src PREFIX "" FILES ${BOOST_JSON_SOURCES})
endif()

if(BOOST_JSON_STANDALONE)
    # Building standalone, out of Boost superproject tree, without Boost as dependency.
    set(CMAKE_CXX_STANDARD 17 CACHE STRING "C++ standard (17 or higher is required)")
    if(CMAKE_CXX_STANDARD LESS 17)
        message(FATAL_ERROR "Boost.JSON requires C++17 in standalone mode, but CMAKE_CXX_STANDARD is '${CMAKE_CXX_STANDARD}'; please set it to 17 or higher")
    endif()
endif()

# TODO: For Boost superproject, do we want to support header-only mode?
#       Then, this needs to read `add_library(boost_json INTERFACE)`
#       and related settings need to be INTERFACE-ed as well.
add_library(boost_json ${BOOST_JSON_HEADERS} ${BOOST_JSON_SOURCES})
set_target_properties(boost_json PROPERTIES EXPORT_NAME json)
add_library(Boost::json ALIAS boost_json)
target_compile_features(boost_json PUBLIC cxx_constexpr)

# TODO: For Boost superproject, this may need to be INTERFACE setting
# Installed targets have different include requirements
target_include_directories(boost_json PUBLIC
        "$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>"
        "$<INSTALL_INTERFACE:include>")

target_compile_definitions(boost_json PUBLIC BOOST_JSON_NO_LIB=1)

if(BUILD_SHARED_LIBS)
    target_compile_definitions(boost_json PUBLIC BOOST_JSON_DYN_LINK=1)
else()
    target_compile_definitions(boost_json PUBLIC BOOST_JSON_STATIC_LINK=1)
endif()

include(CTest)


if(BOOST_JSON_STANDALONE)
    # Building standalone, out of Boost superproject tree, without Boost as dependency.
    target_compile_definitions(boost_json PUBLIC BOOST_JSON_STANDALONE)
elseif(CMAKE_SOURCE_DIR STREQUAL CMAKE_CURRENT_SOURCE_DIR)
    #
    # Building as root project, out of Boost superproject tree, with Boost as dependency.
    # e.g. on Travis or other CI, or when producing Visual Studio Solution and Projects.
    #
    if(${CMAKE_VERSION} VERSION_LESS 3.16)
        message(FATAL_ERROR "Boost.JSON development requires CMake 3.16 or newer.")
    endif()

    get_filename_component(parent_dirname_ "${PROJECT_SOURCE_DIR}/.." ABSOLUTE)
    get_filename_component(parent_dirname_ "${parent_dirname_}" NAME)
    if(parent_dirname_ MATCHES "^[Ll][Ii][Bb][Ss]$")
        set(BOOST_JSON_BOOST_BUILD_TREE ON) # used in limits
        get_filename_component(BOOST_ROOT ../.. ABSOLUTE)
        target_include_directories(boost_json PUBLIC ${BOOST_ROOT})
        target_link_directories(boost_json PUBLIC ${BOOST_ROOT}/stage/lib)
    else()
        find_package(Boost REQUIRED COMPONENTS system container)
        target_link_libraries(boost_json PUBLIC Boost::system Boost::container)
        include(cmake/make_cmake_installer.cmake)
        make_cmake_installer()
    endif()

elseif(BOOST_SUPERPROJECT_VERSION)
    #
    # Building as part of Boost superproject tree, with Boost as dependency.
    #
    # TODO: This CMake support for Boost.Json is currently experimental.
    #       This needs to be reviewed, tested
    target_link_libraries(boost_json
        PUBLIC
            Boost::assert
            Boost::config
            Boost::container
            Boost::core
            Boost::exception
            Boost::system
            Boost::utility
    )

    include(BoostInstall)
    boost_install(TARGETS boost_json HEADER_DIRECTORY include/)
else()
    #
    # Out-of-tree, non-standalone build
    #
    find_package(Boost COMPONENTS system)
    target_link_libraries(boost_json
        PUBLIC
            Boost::container
            Boost::system
    )
endif()

if(BOOST_JSON_ALLOW_TESTS)
    if (BUILD_TESTING)
        add_subdirectory(test)
    endif()

    if (BUILD_TESTING AND NOT BOOST_SUPERPROJECT_VERSION)
            add_subdirectory(bench)
            add_subdirectory(example)
    endif ()
endif()
